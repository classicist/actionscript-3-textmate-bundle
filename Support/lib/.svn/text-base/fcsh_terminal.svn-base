#!/usr/bin/osascript

global FCSHWindowTitle

on run argv
	
	set FCSHPath to item 1 of argv --"fcsh"
	set FCSHArgs to item 2 of argv --"help"
	
	set FCSHWindowTitle to "TextMate fcsh"
	
	set FCSHTab to my find_tab_by_title(FCSHWindowTitle)
	
	if FCSHTab is missing value then
		
		--Start the compile process
		tell application "Terminal"
			
			set FCSHTab to do script "clear"
			delay 1
			do script FCSHPath in FCSHTab
			delay 2
			do script FCSHArgs in FCSHTab
			set custom title of FCSHTab to FCSHWindowTitle
			
		end tell
		
	else
		--Run the compile again
		tell application "Terminal"
			try
				do script "compile 1" in FCSHTab
			end try
		end tell
	end if
	
end run

on find_tab_by_title(TheTabTitle)
	
	-- Find the window/tab that has this custom title.
	
	tell application "Terminal"
		set MyWindowCount to count of windows
		repeat with MyWindowIndex from 1 to MyWindowCount
			set MyWindow to window MyWindowIndex
			try
				set MyTabCount to count of tabs of MyWindow
			on error
				set MyTabCount to 0 -- a non-Terminal window?
			end try
			repeat with MyTabIndex from 1 to MyTabCount
				set MyTab to tab MyTabIndex of MyWindow
				set MyTabTitle to custom title of MyTab
				
				if MyTabTitle is TheTabTitle then
					return MyTab
				end if
				
			end repeat
		end repeat
	end tell
	
	return missing value
end find_tab_by_title